## BeforeCreateOffline, AfterCreateOffline, BeforeDropOffline, AfterDropOffline

These database events fire when a view is taken offline with CreateOffline() or taken back online with DropOffline().

### Usage

```foxpro
PROCEDURE DBC_BeforeCreateOffline( cViewName, cPath )

PROCEDURE DBC_AfterCreateOffline( cViewName, cPath )

PROCEDURE DBC_BeforeDropOffline( cViewName )

PROCEDURE DBC_AfterDropOffline( cViewName )
```
<table border cellspacing=0 cellpadding=0 width=100%>
<tr>
  <td width=32% valign=top>
  <p><b>Parameter</b></p>
  </td>
  <td width=23% valign=top>
  <p><b>Value</b></p>
  </td>
  <td width=45% valign=top>
  <p><b>Meaning</b></p>
  </td>
 </tr>
<tr>
  <td width=32% valign=top>
  <p>cViewName</p>
  </td>
  <td width=23% valign=top>
  <p>Character</p>
  </td>
  <td width=45% valign=top>
  <p>The name of the view.</p>
  </td>
 </tr>
<tr>
  <td width=32% valign=top>
  <p>cPath</p>
  </td>
  <td width=23% valign=top>
  <p>Character</p>
  </td>
  <td width=45% valign=top>
  <p>The path and name for the table representing the offline view.</p>
  </td>
 </tr>
</table>

<table border=0 cellspacing=0 cellpadding=0 width=100%>
<tr>
  <td width=17% valign=top>
<img width=95 height=77 src="bug.gif"></p>
  </td>
  <td width=83%>
  <p>The template generated for the BeforeDropOffline and AfterDropOffline events by the Database Properties dialog has a second parameter, cPath. However, this parameter is never passed a value (PCOUNT() is 1 and cPath is always .F.). So, there's really no such parameter. We're pretty sure a copy and paste operation in VFP's source code was the cause of this, since we've done similar things ourselves once or twice.</p>
  </td>
 </tr>
</table>

As is the case with other before-and-after pairs of events, you can prevent a view from being taken offline or back online by returning .F. in the Before events, while the After events simply receive notification that something happened.

One use for BeforeDropOffline is to ensure there aren't any changes in the offline view, since DropOffline() erases the table representing the offline view and any changes in it are lost. The example shows the code for this.

### Example

```foxpro
* Prevent a view from being taken online if there are any
* changes. This goes in the stored procedures of the database.

PROCEDURE DBC_BeforeDropOffline(cViewName)
LOCAL lnSelect, ;
    llReturn
lnSelect = SELECT()
SELECT 0
USE (cViewName) ADMIN EXCLUSIVE
llReturn = GETNEXTMODIFIED(0, ALIAS(), .T.) = 0
USE
SELECT (lnSelect)
RETURN llReturn

* End of stored procedures.
* Test it by creating a view, taking it offline, making a
* change, and then trying to take it back online.

CREATE SQL VIEW TestView AS SELECT * FROM Customer
SET MULTILOCKS ON
CREATEOFFLINE("TestView")
USE TestView
REPLACE COMPANY WITH "New Company Name"
USE

DROPOFFLINE("TestView")
llOffline = DBGETPROP("TestView", "View", "Offline")
WAIT WINDOW 'TestView is ' + IIF(llOffline, "offline", "online")
```
### See Also

[CreateOffline()](s4g678.md), [Create SQL View](s4g353.md), [Database Events](s4g900.md), [DropOffline()](s4g678.md), [GetNextModified()](s4g502.md), [Use](s4g424.md)