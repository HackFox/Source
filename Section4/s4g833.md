## ADLLs()

This function was added in VFP 7 to provide an easy way to find out what API functions have been declared.

### Usage

```foxpro
nFunctionCount = ADLLS( aFunctionList )
```
<table border cellspacing=0 cellpadding=0 width=100%>
<tr>
  <td width=32% valign=top>
  <p><b>Parameter</b></p>
  </td>
  <td width=23% valign=top>
  <p><b>Value</b></p>
  </td>
  <td width=45% valign=top>
  <p><b>Meaning</b></p>
  </td>
 </tr>
<tr>
  <td width=32% valign=top>
  <p>aFunctionList</p>
  </td>
  <td width=23% valign=top>
  <p>Name</p>
  </td>
  <td width=45% valign=top>
  <p>An array to hold the list of declared API functions.</p>
  </td>
 </tr>
<tr>
  <td width=32% valign=top>
  <p>nFunctionCount</p>
  </td>
  <td width=23% valign=top>
  <p>Number</p>
  </td>
  <td width=45% valign=top>
  <p>The number of API functions declared.</p>
  </td>
 </tr>
</table>

VFP lets you use functions from the Windows API by declaring them (using the DECLARE-DLL command). Prior to VFP 7, the only way to find out programmatically which functions had already been declared was to LIST STATUS to a file, then parse the file. Nasty and error-prone.

ADLLS() makes it easy. It fills an array with the list of declared functions. The new array has three columns. The first contains the actual name of the function. The second contains the alias assigned the function when it was declared&mdash;aliases are assigned using the AS clause of DECLARE-DLL. The third column contains the full path and filename of the library containing the function. What's cool here is that even when you declare a function with the IN WIN32API clause, ADLLS() digs up the name and path of the actual DLL file containing the function.

While you can use this function to figure out whether a particular API function has been declared before doing so, there's no penalty for declaring a function more than once.

We do wish this function returned one more piece of information: the list of parameter types declared for the function. The problem is that sometimes you might choose to declare some parameter types differently from the actual Win32API declaration, perhaps because VFP handles that type poorly. But if you have two different declarations, calling the function could result in an error. As we say above, since there's no penalty for the declaring the function more than once, we feel it's better to declare it each time you use it.

### Example

```foxpro
* See whether a particular function was declared.
* Grab the alias, if so, so we can use the function
* without worrying what name it was declared with.
LOCAL nDLLCount, aDLLList[1], nItem, cGSCAlias
nDLLCount = ADLLS( aDLLList )
nItem = ASCAN( aDLLList, "GetSysColor", -1, -1, 1, 14)
IF nItem = 0
  * Declare it
  DECLARE INTEGER GetSysColor IN WIN32API INTEGER nElement
  cGSCAlias = "GetSysColor"
ELSE
  * Grab the defined alias for the function
  cGSCAlias = aDLLList[ nItem, 2 ]
ENDIF

* Now run it
cCommand = cGSCAlias + "(13)"
nResult = EVALUATE(cCommand)
WAIT WINDOW TRANSFORM(nResult)
```

The example takes advantage of several of the new capabilities of ASCAN()&mdash;specifying a particular column, forcing a search to be exact (in the VFP sense), and returning the row number rather than the element number. Because API functions are case-sensitive, the example doesn't specify a case-insensitive search, another of the new abilities of ASCAN().

### See Also

[AScan()](s4g216.md), [Clear DLLS](s4g796.md), [Declare-DLL](s4g281.md), [List Status](s4g111.md)