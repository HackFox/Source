## CDoW(), CMonth(), Day(), Month(), Year(), Quarter()

These functions return the character and numeric equivalents of the date or datetime supplied.

### Usage

```foxpro
cRetVal = CDOW( dDate | tDateTime )
cRetVal = CMONTH( dDate | tDateTime )
nRetVal = DAY( dDate | tDateTime )
nRetVal = MONTH( dDate | tDateTime )
nRetVal = YEAR( dDate | tDateTime )
nRetVal = QUARTER( dDate | tDateTime [, nFirstMonth ] )
```

CDoW() and CMonth() display the current day ("Saturday") and month ("December"). The return values are always in Proper format: capitalized first letter, lowercase for the remainder. Use these functions for output only, and not for internal business logic, if your application will be used in multi-lingual settings where one man's Thursday can be another's Donnerstag.

Day(), Month() and Year() return the numeric equivalents of the day of the month, month and year, based on the date or datetime supplied. They are absolute&mdash;they are based on the Gregorian calendar, a generally agreed-upon standard. Year() always returns the full year, including century, regardless of the setting of CENTURY. Two other similar functions, DoW() and Week(), are discussed in their own section, as they depend on the system settings of the "first week of the year" and the "first day of the week."

Quarter(), new in VFP 7, returns the numeric quarter of the year for the specified date or datetime. The optional nFirstMonth parameter lets you specify the starting month for the year.

Be forewarned. The functions in this group that return numeric values return them as numbers, not integers. That means that if you SET FIXED ON, you're going to see decimal places in the day, month, year and quarter. Probably not what you expected.

### Example

```foxpro
? CDOW(DATE())          && The equivalent of "What day is it?"
? CMONTH(DATETIME())    && and "What month is it?"
? DAY({^ 1995/06/15})      && Returns 15
? MONTH({^1995/06/15})     && Returns 6
? YEAR({^1995/06/15})      && Returns 1995
? QUARTER({^1995/06/15})   && Returns 2
? QUARTER({^1995/06/15}, 7)   && Returns 4
```
### See Also

[DoW()](s4g288.md), [Date()](s4g031.md), [DateTime()](s4g031.md), [DMY()](s4g032.md), [MDY()](s4g032.md), [GoMonth()](s4g033.md), [Set Century](s4g035.md), [Set Date](s4g035.md), [Set FDoW](s4g295.md), [Set FWeek](s4g295.md), [Set Fixed](s4g133.md), [Set Mark To](s4g035.md), [Sys(1)](s4g037.md), [Sys(10)](s4g037.md), [Sys(11)](s4g037.md), [Week()](s4g288.md)