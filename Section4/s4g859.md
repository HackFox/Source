## ASessions()

This is a function that was sorely missed in VFP 6 and earlier versions. Added in VFP 7, it fills an array with the list of data sessions in use.

### Usage

```foxpro
nSessionCount = ASESSIONS( aSessionList )
```
<table border cellspacing=0 cellpadding=0 width=100%>
<tr>
  <td width=32% valign=top>
  <p><b>Parameter</b></p>
  </td>
  <td width=23% valign=top>
  <p><b>Value</b></p>
  </td>
  <td width=45% valign=top>
  <p><b>Meaning</b></p>
  </td>
 </tr>
<tr>
  <td width=32% valign=top>
  <p>aSessionList</p>
  </td>
  <td width=23% valign=top>
  <p>Array</p>
  </td>
  <td width=45% valign=top>
  <p>An array to hold the list of sessions. The array is created if it doesn't already exist and resized if it does. The resulting array is one-dimensional.</p>
  </td>
 </tr>
<tr>
  <td width=32% valign=top>
  <p>nSessionCount</p>
  </td>
  <td width=23% valign=top>
  <p>Numeric</p>
  </td>
  <td width=45% valign=top>
  <p>The number of open data sessions.</p>
  </td>
 </tr>
</table>

Every now and then, you need to go through all the open data sessions to do some kind of task, such as closing all open tables and rolling back open transactions (say, in an error situation) or closing one table wherever it's open. In older versions of VFP, this was difficult because there was no way to know how many data sessions were in use and what data session numbers they were assigned. You could loop, issuing SET DATASESSION until you found an unused data session number (catching the resulting error), but due to the way data session numbers are assigned, that didn't guarantee visiting every session.

No more. ASessions() does for data sessions what AUsed() does for work areas: It fills an array with the list of data sessions in use and returns the count. 

At first glance, you might wonder why it isn't enough to just be able to get the number of open data sessions. After all, they're assigned numbers, starting with 1 for the default, public, always active data session. However, once you create a data session, it keeps its session number, even if data sessions created earlier are closed. So there can be gaps in the list. For example, suppose you open three forms with private data sessions&mdash;they're assigned session numbers 2, 3 and 4. If you then close the first of those forms, the list of data sessions in use is 1 (the default), 3 and 4. Issuing ASessions() at this point would return 3 and create a three-element array containing 1, 3 and 4.

To make matters more interesting, VFP always fills in holes in the sequence, if possible. So, continuing the previous example, if you run another form at this point, it's assigned data session 2. Issuing ASessions() at this point returns 4 and creates a four-element array with the values 1, 3, 4 and 2.

### Example

```foxpro
* You're most likely to use the results of
* ASessions() to drive a loop.
LOCAL nOldSession
LOCAL nSessionCount, aSessionList[1], nSession
LOCAL nTableCount, aTables[1], nTable

nOldSession = SET("DATASESSION")
nSessionCount = ASESSIONS( aSessionList )

FOR EACH nSession in aSessionList
   * Switch sessions
   SET DATASESSION TO nSession
   * Get a list of open tables.
   nTableCount = AUSED( aTables )
   * Do something for each data session, like ROLLBACK
   FOR nTable = 1 TO nTableCount
      * Do something with each table, like TABLEREVERT().
      WAIT WINDOW ALIAS( nTable )
   ENDFOR
ENDFOR

* Return to original data session
SET DATASESSION TO (nOldSession)
```
### See Also

[ADatabases()](s4g666.md), [Array Manipulation](s4g282.md), [AUsed()](s4g290.md), [Set DataSession](s4g392.md)